/** single Grip Tile Clock Counter. Count tile edges while in current grip.
    \symbol GT
    \color #c3e
 */
element GTCC : HOpUnary + Grip + Fail {

  typedef Unsigned(XBarSpace2D.cGRIPBITS) MyGripVal;
  MyGripVal mLastGrip = MyGripVal.maxof;

  typedef XTimer(3u,5u,1u) ChangeTimer; //< for debouncing edges
  ChangeTimer mEdgeTimer[2]; // [0] front, [1] back
  ChangeTimer mRegripTimer;

  typedef GreyClok.ClokState ClokState;
  constant ClokState cCLOK_NONE = GreyClok.cCLOK_NONE;
  ClokState mNewCloks[2] = {cCLOK_NONE, cCLOK_NONE}; // [0] front, [1] rear
  ClokState mLastCloks[2] = {cCLOK_NONE, cCLOK_NONE};

  typedef Unsigned(6u) TileCount;
  TileCount mTilesInGrip;
  
  Void behave() {

    /// RUN GRIP TIMER
    Unsigned gripnum = getGripNumber(); // our 'official' current grip
    if (gripnum == mLastGrip) mRegripTimer.reset();
    else if (mRegripTimer.countAlarm()) {
      mLastGrip = (MyGripVal) gripnum;
      mD&&pR("GTCCX12")&&pR(self);
      mTilesInGrip = 0u;
    }

    /// RUN EDGE TIMERS
    for (Unsigned i = 0u; i < 2u; ++i) {
      if (mNewCloks[i] == mLastCloks[i]) mEdgeTimer[i].reset();
      else if (mNewCloks[i] != GreyClok.cCLOK_NONE && mEdgeTimer[i].countAlarm()) {
        if (i == 0u && mLastCloks[i] != GreyClok.cCLOK_NONE) ++mTilesInGrip;
        mLastCloks[i] = mNewCloks[i];
      }
    }

    /// AND DO NORMAL BUSINESS
    super.behave();
  }

  //// HOpArgsT API
  @Override
  virtual Int sigToSlot(SlotSig sig, Unsigned gripnum) {
    mD&&pR("GTCS2S10")&&pR(self);

    BeeveeBIM bim;
    TapIdx idx = sig.mSource;
    if (idx == bim.getTapIdxFromClassId(CLOK.classidof)) // clock edges
      return 0;             

    return Int.maxof;           // or don't want
  }

  @Override
  virtual Bool execute(SlotSig & result) {
    mD&&pR("GTCCX10")&&pR(self);
    SlotSig & sig = getSlotSig(0);

    if (!sig.isOccupied()) return false;
    mD&&pR("GTCCX1010")&&pR(self);

    /// PICK UP CLOCK SIG
    Unsigned val = sig.getValueUnsigned();
    for (Unsigned i = 0u; i < 2u; ++i) {
      mNewCloks[i] = (ClokState) (val%4u);
      val = val/4u;
    }

    return false; // NOT REACHED
 }
}

